cmake_minimum_required(VERSION 3.5.2 FATAL_ERROR)

project(CMakeLesson11)

# CMake can execute a given command via add_custom_command or add_custom_target.

set(OUTPUT_FILE "Script/created_file.txt")

# add_custom_command
# See https://cmake.org/cmake/help/v3.2/command/add_custom_command.html
# OUTPUT - files that are generated by command execution. There could be more than one file. 
#          Command will be executed if file doesn't exist. Moreover if any target depends on
#          this file, commands will be invoked.
# COMMAND - commands to execute. There could be more than one command. Each one starts with COMMAND.
# WORKING_DIRECTORY - the directory location from where commands will be executed.
# COMMENT - the comment that will be displayed before commands execution.
add_custom_command(
  OUTPUT ${OUTPUT_FILE}
  COMMAND "./script.sh"
  WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}/Script"
  COMMENT "Comment before launching script"
)

# add_custom_target
# See https://cmake.org/cmake/help/v3.2/command/add_custom_target.html
#
# customTargetAll - a name of the target
# ALL - adds this target to the default target, therefore the target will be run every time.
# DEPENDS - this target requires files ${OUTPUT_FILE}. So the command above will be 
#           invoke, if file doesn't exist.
add_custom_target(
    customTargetAll ALL
    DEPENDS ${OUTPUT_FILE}
    )

########################

# add_custom_target
# See https://cmake.org/cmake/help/v3.2/command/add_custom_target.html
# customTarget - it is a name of target
# All arguments are the same as in add_custom_command.
add_custom_target(
  customTarget
  COMMAND "./script2.sh"
  WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}/Script"
  COMMENT "Comment before launching script"
)

# NOTE:
# You can invoke this target after generating via: make customTarget


# Author: Marcin Serwach
# Tutorial: https://github.com/iblis-ms/tutorials/tree/master/Cpp/cmake
